isEmpty(QTNPROPERTY_BIN) {
    include($$PWD/Internal/BaseConfig.pri)
}

QTNPROPERTY_PATH = $$PWD

isEmpty(QTNPROPERTY_LIB) {
    QTNPROPERTY_LIB = $$QTNPROPERTY_BIN
}

QTNPROPERTY_LIBNAME = QtnProperty
qtnproperty_dynamic:QTNPROPERTY_LIBNAME = $$join(QTNPROPERTY_LIBNAME, , , 2)

win32 {
    msvc:PRE_TARGETDEPS += $$QTNPROPERTY_LIB/$$QTNPROPERTY_LIBNAME.lib
    else:PRE_TARGETDEPS += $$QTNPROPERTY_LIB/lib$$QTNPROPERTY_LIBNAME.a
}

qtnproperty_dynamic {
    DEFINES += QTN_DYNAMIC_IMPORT
    macx {
        DYNAMIC_LIBS.files += $$QTNPROPERTY_LIB/lib$$QTNPROPERTY_LIBNAME.dylib
    }
} else {
    unix {
        PRE_TARGETDEPS += $$QTNPROPERTY_LIB/lib$$QTNPROPERTY_LIBNAME.a
    }
}

LIBS += -L$$QTNPROPERTY_LIB
LIBS += -l$$QTNPROPERTY_LIBNAME

INCLUDEPATH += $$QTNPROPERTY_PATH
